Common Beg
    #version 330 core

    #define LIGHT_TYPE_DIRECT_  0
    #define LIGHT_TYPE_POINT_   1
    #define LIGHT_TYPE_SPOT_    2

    uniform vec3    camera_pos_;
    uniform mat4    matrix_mvp_;
    uniform int     light_type_;

    uniform	sampler2D gbuffer_position_;
    uniform	sampler2D gbuffer_diffuse_;
    uniform	sampler2D gbuffer_normal_;
    uniform sampler2D screen_sao_;

    uniform sampler2D   shadow_map_direct_0_;
    uniform samplerCube shadow_map_point_0_;
    uniform sampler2D   shadow_map_spot_0_;

    FShader Beg
        int CheckInView(vec4 vec)
        {
            int ret = 0;
            if		(vec.x < -vec.w) ret |= 1;
            else if (vec.x >  vec.w) ret |= 2;
            else if (vec.y < -vec.w) ret |= 4;
            else if (vec.y >  vec.w) ret |= 8;
            else if (vec.z < -vec.w) ret |= 16;
            else if (vec.z >  vec.w) ret |= 32;
            return ret;
        }

        //	计算漫反射缩放因子
        float CalculateDiffuseScale(vec3 fragNormal, vec3 lightNormal, vec3 cameraNormal)
        {
            float v = max(dot(fragNormal, lightNormal), 0) * 0.5f + 0.5f;
            if (v < 0.55)       return 0.0;
            else if (v < 0.80)  return 0.5;
            else                return 1;
        }

        //	计算距离衰减缩放因子
        float CalculateDistanceScale(vec3 fragPosition, vec3 lightPosition, float k0, float k1, float k2)
        {
            float len = length(fragPosition - lightPosition);
            return 1.0f / (k0 + k1 * len + k2 * len * len);
        }

        //	计算外锥衰减缩放因子
        float CalculateOutConeScale(float inCone, float outCone, vec3 lightNormal, vec3 lightToFrag)
        {
            float cone = dot(lightNormal, lightToFrag);
            return clamp((cone - outCone) / (inCone - outCone), 0, 1);
        }
    FShader End

Common End

Pass Beg
	CullFace    Front
    BlendMode   One One
	RenderType  Light
	DrawType    Index

	VShader Beg
		layout(location = 0) in vec3 a_v_;

        layout (std140) uniform LightSpot_ {
			float mK0;
            float mK1;
            float mK2;
			float mInCone;
            float mOutCone;
			mat4 mMatrix;
			vec3 mNormal;
			vec3 mAmbient;
			vec3 mDiffuse;
			vec3 mSpecular;
			vec3 mPosition;
        } light_spot_;

		out V_OUT_ {
            vec4 mMVPPos;
		} v_out_;

		void main()
		{
            switch (light_type_)
            {
            case LIGHT_TYPE_DIRECT_:
                gl_Position    = vec4(a_v_,1);
                v_out_.mMVPPos = gl_Position;
                break;
            case LIGHT_TYPE_POINT_:
                gl_Position     = matrix_mvp_ * vec4(a_v_, 1);
                v_out_.mMVPPos  = gl_Position;
                break;
            case LIGHT_TYPE_SPOT_:
                vec3 normal     = a_v_ - vec3(0, 0, 1);
                vec3 offset     = normal * (light_spot_.mOutCone - 0.5f);
                vec3 point      = a_v_ + offset;

                gl_Position     = matrix_mvp_ * vec4(point, 1);
                v_out_.mMVPPos  = gl_Position;
                break;
            }
		}
	VShader End

	FShader Beg
        layout (std140) uniform LightDirect_ {
			mat4 mMatrix;
			vec3 mNormal;
			vec3 mAmbient;
			vec3 mDiffuse;
			vec3 mSpecular;
			vec3 mPosition;
        } light_direct_;

        layout (std140) uniform LightPoint_ {
			float mFar, mNear;
            float mK0, mK1, mK2;
            vec3 mAmbient;
            vec3 mDiffuse;
            vec3 mSpecular;
            vec3 mPosition;
        } light_point_;

        layout (std140) uniform LightSpot_ {
			float mK0;
            float mK1;
            float mK2;
			float mInCone;
            float mOutCone;
			mat4 mMatrix;
			vec3 mNormal;
			vec3 mAmbient;
			vec3 mDiffuse;
			vec3 mSpecular;
			vec3 mPosition;
        } light_spot_;

        in V_OUT_ {
            vec4 mMVPPos;
        } v_out_;

		out vec4 color_;

        float CalculateDirectShadow(const vec3 position)
        {
            vec4 pos = light_direct_.mMatrix * vec4(position, 1);
            if (CheckInView(pos) != 0) { return 0; }
            pos.xyz = pos.xyz / pos.w * 0.5f + 0.5f;
			float zorder = pos.z - 0.001f;
            float shadow = 0.0f;
            vec2 texstep = 1.0f / textureSize(shadow_map_direct_0_, 0);
            float depth = texture(shadow_map_direct_0_, pos.xy + vec2(-texstep.x,  texstep.y)).r; shadow += zorder < depth? 1: 0;
                  depth = texture(shadow_map_direct_0_, pos.xy + vec2( 0,          texstep.y)).r; shadow += zorder < depth? 1: 0;
                  depth = texture(shadow_map_direct_0_, pos.xy + vec2( texstep.x,  texstep.y)).r; shadow += zorder < depth? 1: 0;
                  depth = texture(shadow_map_direct_0_, pos.xy + vec2(-texstep.x,  0		)).r; shadow += zorder < depth? 1: 0;
                  depth = texture(shadow_map_direct_0_, pos.xy + vec2( 0,          0	    )).r; shadow += zorder < depth? 1: 0;
                  depth = texture(shadow_map_direct_0_, pos.xy + vec2( texstep.x,  0	    )).r; shadow += zorder < depth? 1: 0;
                  depth = texture(shadow_map_direct_0_, pos.xy + vec2(-texstep.x, -texstep.y)).r; shadow += zorder < depth? 1: 0;
                  depth = texture(shadow_map_direct_0_, pos.xy + vec2( 0,		  -texstep.y)).r; shadow += zorder < depth? 1: 0;
                  depth = texture(shadow_map_direct_0_, pos.xy + vec2( texstep.x, -texstep.y)).r; shadow += zorder < depth? 1: 0;
            return shadow / 9.0f;
        }

		float CalculateSpotShadow(const vec3 position)
		{
			vec4 pos = light_spot_.mMatrix * vec4(position, 1);
            if (CheckInView(pos) != 0) { return 0; }
            pos.xyz = pos.xyz / pos.w * 0.5f + 0.5f;
			float zorder = pos.z - 0.001f;
            float shadow = 0.0f;
            vec2 texstep = 1.0f / textureSize(shadow_map_spot_0_, 0);
            float depth = texture(shadow_map_spot_0_, pos.xy + vec2(-texstep.x,  texstep.y)).r; shadow += zorder < depth? 1: 0;
                  depth = texture(shadow_map_spot_0_, pos.xy + vec2( 0,          texstep.y)).r; shadow += zorder < depth? 1: 0;
                  depth = texture(shadow_map_spot_0_, pos.xy + vec2( texstep.x,  texstep.y)).r; shadow += zorder < depth? 1: 0;
                  depth = texture(shadow_map_spot_0_, pos.xy + vec2(-texstep.x,  0        )).r; shadow += zorder < depth? 1: 0;
                  depth = texture(shadow_map_spot_0_, pos.xy + vec2( 0,          0        )).r; shadow += zorder < depth? 1: 0;
                  depth = texture(shadow_map_spot_0_, pos.xy + vec2( texstep.x,  0        )).r; shadow += zorder < depth? 1: 0;
                  depth = texture(shadow_map_spot_0_, pos.xy + vec2(-texstep.x, -texstep.y)).r; shadow += zorder < depth? 1: 0;
                  depth = texture(shadow_map_spot_0_, pos.xy + vec2( 0,	        -texstep.y)).r; shadow += zorder < depth? 1: 0;
                  depth = texture(shadow_map_spot_0_, pos.xy + vec2( texstep.x, -texstep.y)).r; shadow += zorder < depth? 1: 0;
            return shadow / 9.0f;
		}

		float CalculatePointShadow(const vec3 position)
		{
			vec3 normal = position - light_point_.mPosition;
			float z 	= texture(shadow_map_point_0_, normal).r;
			return length(normal) > z * light_point_.mFar + 1? 0 : 1;
		}

		vec3 CalculateDirect(const vec3 fragCoord, const vec3 fragNormal, const vec3 cameraNormal, const vec3 diffuse, const float occlus, const vec2 uv)
		{
			float shadow = CalculateDirectShadow(fragCoord);
			float diff   = CalculateDiffuseScale(fragNormal, -light_direct_.mNormal, cameraNormal);
			return diffuse * light_direct_.mAmbient * (1   - occlus)
                 + diffuse * light_direct_.mDiffuse * diff * shadow;
		}

        vec3 CalculatePoint(const vec3 fragCoord, const vec3 fragNormal, const vec3 cameraNormal, const vec3 diffuse, const float occlus, const vec2 uv)
        {
            vec3 fragToLight = normalize(light_point_.mPosition - fragCoord);
			float shadow     = CalculatePointShadow(fragCoord);
			float diff       = CalculateDiffuseScale(fragNormal, fragToLight, cameraNormal);
			//	距离衰减
			float distance = CalculateDistanceScale(fragCoord, 
													light_point_.mPosition, 
													light_point_.mK0, 
													light_point_.mK1,
													light_point_.mK2);
			return (diffuse * light_point_.mAmbient * (1   - occlus)
                  + diffuse * light_point_.mDiffuse * diff * shadow) * distance;
        }

		vec3 CalculateSpot(const vec3 fragCoord, const vec3 fragNormal, const vec3 cameraNormal, const vec3 diffuse, const float occlus, const vec2 uv)
		{
			vec3 fragToLight = normalize(light_spot_.mPosition - fragCoord);
			float shadow     = CalculateSpotShadow(fragCoord);
			float diff       = CalculateDiffuseScale(fragNormal, fragToLight, cameraNormal);
			//	光锥衰减
			float weight = CalculateOutConeScale(light_spot_.mInCone, 
                                                 light_spot_.mOutCone, 
                                                 light_spot_.mNormal, -fragToLight);
			//	距离衰减
			float distance = CalculateDistanceScale(fragCoord, 
                                                    light_spot_.mPosition, 
                                                    light_spot_.mK0, 
                                                    light_spot_.mK1, 
                                                    light_spot_.mK2);
			return (diffuse * light_spot_.mAmbient * (1   - occlus)
                  + diffuse * light_spot_.mDiffuse * diff * shadow) * weight * distance;
		}

		void main()
		{
            vec2 uv = v_out_.mMVPPos.xy / v_out_.mMVPPos.w * 0.5 + 0.5;

            vec3  position  = vec3(texture(gbuffer_position_, uv));
            vec3  diffuse   = vec3(texture(gbuffer_diffuse_, uv));
            vec3  normal    = vec3(texture(gbuffer_normal_, uv));
            float occlus    = texture(screen_sao_, uv).r;

            switch (light_type_)
            {
            case LIGHT_TYPE_DIRECT_:
                color_ = vec4(CalculateDirect(position, normal, normalize(camera_pos_ - position), diffuse, occlus, uv), 1);
                break;
            case LIGHT_TYPE_POINT_:
                color_ = vec4(CalculatePoint(position, normal, normalize(camera_pos_ - position), diffuse, occlus, uv), 1);
                break;
            case LIGHT_TYPE_SPOT_:
                color_ = vec4(CalculateSpot(position, normal, normalize(camera_pos_ - position), diffuse, occlus, uv), 1);
                break;
            }
		}
	FShader End
Pass End

Pass Beg
	CullFace    Front
	RenderType  Light
	DrawType    Index

	VShader Beg
		layout(location = 0) in vec3 a_v_;

        layout (std140) uniform LightSpot_ {
			float mK0;
            float mK1;
            float mK2;
			float mInCone;
            float mOutCone;
			mat4 mMatrix;
			vec3 mNormal;
			vec3 mAmbient;
			vec3 mDiffuse;
			vec3 mSpecular;
			vec3 mPosition;
        } light_spot_;

		out V_OUT_ {
            vec4 mMVPPos;
		} v_out_;

		void main()
		{
            switch (light_type_)
            {
            case LIGHT_TYPE_DIRECT_:
                gl_Position     = vec4(a_v_, 1);
                v_out_.mMVPPos  = gl_Position;
                break;
            case LIGHT_TYPE_POINT_:
                gl_Position     = matrix_mvp_ * vec4(a_v_, 1);
                v_out_.mMVPPos  = gl_Position;
                break;
            case LIGHT_TYPE_SPOT_:
                vec3 normal     = a_v_ - vec3(0, 0, 1);
                vec3 offset     = normal * (light_spot_.mOutCone - 0.5f);
                vec3 point      = a_v_ + offset;

                gl_Position     = matrix_mvp_ * vec4(point, 1);
                v_out_.mMVPPos  = gl_Position;
                break;
            }
		}
	VShader End

	FShader Beg
        layout (std140) uniform LightDirect_ {
			mat4 mMatrix;
			vec3 mNormal;
			vec3 mAmbient;
			vec3 mDiffuse;
			vec3 mSpecular;
			vec3 mPosition;
        } light_direct_;

        layout (std140) uniform LightPoint_ {
			float mFar, mNear;
            float mK0, mK1, mK2;
            vec3 mAmbient;
            vec3 mDiffuse;
            vec3 mSpecular;
            vec3 mPosition;
        } light_point_;

        layout (std140) uniform LightSpot_ {
			float mK0;
            float mK1;
            float mK2;
			float mInCone;
            float mOutCone;
			mat4 mMatrix;
			vec3 mNormal;
			vec3 mAmbient;
			vec3 mDiffuse;
			vec3 mSpecular;
			vec3 mPosition;
        } light_spot_;

        in V_OUT_ {
            vec4 mMVPPos;
        } v_out_;

		out vec4 color_;

		vec3 CalculateDirect(const vec3 fragCoord, const vec3 fragNormal, const vec3 cameraNormal, const vec3 diffuse, const float occlus, const vec2 uv)
		{
			float diff   = CalculateDiffuseScale(fragNormal, -light_direct_.mNormal, cameraNormal);
			return diffuse * light_direct_.mAmbient * (1 - occlus)
                 + diffuse * light_direct_.mDiffuse * diff;
		}

        vec3 CalculatePoint(const vec3 fragCoord, const vec3 fragNormal, const vec3 cameraNormal, const vec3 diffuse, const float occlus, const vec2 uv)
        {
            vec3 fragToLight = normalize(light_point_.mPosition - fragCoord);
			float diff       = CalculateDiffuseScale(fragNormal, fragToLight, cameraNormal);
			//	距离衰减
			float distance = CalculateDistanceScale(fragCoord, 
													light_point_.mPosition, 
													light_point_.mK0, 
													light_point_.mK1,
													light_point_.mK2);
			return (diffuse * light_point_.mAmbient * (1 - occlus)
                  + diffuse * light_point_.mDiffuse * diff) * distance;
        }

		vec3 CalculateSpot(const vec3 fragCoord, const vec3 fragNormal, const vec3 cameraNormal, const vec3 diffuse, const float occlus, const vec2 uv)
		{
			vec3 fragToLight = normalize(light_spot_.mPosition - fragCoord);
			float diff       = CalculateDiffuseScale(fragNormal, fragToLight, cameraNormal);
			//	光锥衰减
			float weight = CalculateOutConeScale(light_spot_.mInCone, 
                                                 light_spot_.mOutCone, 
                                                 light_spot_.mNormal, -fragToLight);
			//	距离衰减
			float distance = CalculateDistanceScale(fragCoord, 
                                                    light_spot_.mPosition, 
                                                    light_spot_.mK0, 
                                                    light_spot_.mK1, 
                                                    light_spot_.mK2);
			return (diffuse * light_spot_.mAmbient * (1 - occlus)
                  + diffuse * light_spot_.mDiffuse * diff) * weight * distance;
		}

		void main()
		{
            vec2 uv = v_out_.mMVPPos.xy / v_out_.mMVPPos.w * 0.5 + 0.5;

            vec3  position  = vec3(texture(gbuffer_position_, uv));
            vec3  diffuse   = vec3(texture(gbuffer_diffuse_, uv));
            vec3  normal    = vec3(texture(gbuffer_normal_, uv));
            float occlus    = texture(screen_sao_, uv).r;

            switch (light_type_)
            {
            case LIGHT_TYPE_DIRECT_:
                color_ = vec4(CalculateDirect(position, normal, normalize(camera_pos_ - position), diffuse, occlus, uv), 1);
                break;
            case LIGHT_TYPE_POINT_:
                color_ = vec4(CalculatePoint(position, normal, normalize(camera_pos_ - position), diffuse, occlus, uv), 1);
                break;
            case LIGHT_TYPE_SPOT_:
                color_ = vec4(CalculateSpot(position, normal, normalize(camera_pos_ - position), diffuse, occlus, uv), 1);
                break;
            }
		}
    FShader End
Pass End